plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.mikepenz.aboutlibraries.plugin'
}

def apiPropertiesFile = rootProject.file("api.properties")
def apiProperties = new Properties()
apiProperties.load(new FileInputStream(apiPropertiesFile))

android {
    compileSdkVersion 31

    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }

    defaultConfig {
        applicationId "de.seemoo.at_tracking_detection"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 32
        versionName "1.3.3"

        buildConfigField "String", "API_KEY", apiProperties["API_KEY"]
        buildConfigField "String", "API_BASE_ADDRESS", apiProperties["API_BASE_ADDRESS"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".release"
            resValue "bool", "debug", "false"
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            debuggable true
            resValue "bool", "debug", "true"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.2.0-alpha08"
    }


    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
//        androidTest.assets.srcDirs += files("/Users/seemoo/Work/Research/AirGuard/airguard-app/app/schemas/de.seemoo.at_tracking_detection.database.AppDatabase".toString())
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}

aboutLibraries {
    exclusionPatterns = [~"androidx.*", ~"com_android.*", ~/de_seemoo.*/, ~"com_google.*", ~"org_jetbrains.*"]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.github.bastienpaulfr:Treessence:1.0.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.5'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.5'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'

    implementation 'com.github.AppIntro:AppIntro:6.1.0'

    implementation 'org.osmdroid:osmdroid-android:6.1.11'

    implementation 'com.github.ybq:Android-SpinKit:1.4.0'

    implementation 'com.mikepenz:aboutlibraries:8.9.3'


    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.room:room-testing:2.4.2"
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    //Finds memory leaks while running the app in Debug mode
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'

    //Compose
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.1.1'
    // Animations
    implementation 'androidx.compose.animation:animation:1.1.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.1.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.1'
    // When using a MDC theme
    implementation "com.google.android.material:compose-theme-adapter:1.1.7"

}